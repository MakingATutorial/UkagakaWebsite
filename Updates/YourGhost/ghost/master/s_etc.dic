//---------------------------Miscellaneous Functions--------------------------

//This .dic file is concerned with a number of functions that didn't quite fit in anywhere else. If you're adding new functions, this may be the place to do it! This also has the functions related to updating your ghost, among others. Most of this will just be replacing placeholder dialogue with dialogue more appropriate for your ghost, and this file is fairly long. On the positive side, you'll probably only have to go through it once.
//Make sure to read the walkthrough page (http://www.ashido.com/ukagaka/walkthrough.html) for more details about how to properly format ghost dialogue!
//I will reiterate that you should be editing these in Notepad++, and in particular, you should set the Language to C (or Java I guess) to properly highlight all the text. It will make things A LOT EASIER for you, trust me! It will also help you keep track of your brackets in case you forget some. If you click on a line with a bracket in Notepad++, it should highlight where the other bracket ends, or turn purple if there's no finishing bracket (which you should definitely fix!)
//It will also highlight my commented lines in green, so you'll know where they are. Any line beginning with // is a commented line that the code will not read. It's just for your reference!


//--OnFileDrop2

//OnFileDrop2 handles how your ghost will set an image as the desktop background when someone drags and drops an image on them. You don't need to touch any of the coding here, just replace the dialogue as needed and leave the \q options alone. More about \q options in bootend.dic and in the walkthrough.

OnFileDrop2
{
	lastinteracted = 0
	if LV == 0
	{
		_filepath = SPLITPATH(reference0)
		_filename = _filepath[2]
		_fileext = TOLOWER(_filepath[3])
		case _fileext
		{
			when ".jpg", ".jpeg", ".bmp" //These are the formats your ghost will recognize as image files to set as the background. I tested this with .png files and it does not work, so don't bother adding that as an option.
			{
			Wallpaperfile = reference0
			
			"\0\s[110]Oh, a picture?\w8\w8\s[1020] Uh, sure, I think I could set that as your wallpaper.\w8\w8\s[208] How do you want it?\n\n[half]/ 
			\![*]\q[Centered,SetWallpaper_0]\n/
			\![*]\q[Tiled,SetWallpaper_1]\n/
			\![*]\q[Stretched,SetWallpaper_2]\n\n[half]/
			\![*]\q[No thanks,SetWallpaper_Cancel]\e"
			}
			when ".mid", ".wav", ".mp3", ".wma", ".au" //Other file formats you might want to give your ghost a specific response to.
			{
				"\s[103]Oh hey, is this music?\w8\w8\s[106] Unfortunately, I don't uh...\w8\s[107] know how to read a file like this.\w8\w8\s[0] Maybe someone else could play it for you?\e"
			}
			others //Any other file format. As a note here, if you drop something ghost-related on them that does not have an install.txt, then they won't install it and will just say this response. If you are testing your balloon or nar files and you get this response instead of a proper installation, you should check and make sure you have an install.txt in the file!
			{
				"\0\s[106]Sorry, I don't recognize that file type.\w8\w8\s[107] I'm not very good with computers, to be honest with you.\e"
			}		
		}
	}
	elseif LVCheck == 1
	{
		_filepath = SPLITPATH(reference0)
		_filename = _filepath[2]
		_fileext = TOLOWER(_filepath[3])
		case _fileext
		{
			when ".jpg", ".jpeg", ".bmp"
			{
			Wallpaperfile = reference0
			
			"\0\s[510]\i[0]...How do you want your wallpaper set?\n\n[half]/ 
			\![*]\q[Centered,SetWallpaper_0]\n/
			\![*]\q[Tiled,SetWallpaper_1]\n/
			\![*]\q[Stretched,SetWallpaper_2]\n\n[half]/
			\![*]\q[Forget it,SetWallpaper_Cancel]\e"
			}
			others
			{
				"\0\s[510]\i[0]...Sorry, I can't do anything with that file.\e"
			}
		}			
	}
	else
	{
			"\s[400]\i[0]Hmph.\w8 I'm not doing anything with it.\e"
			"\s[400]\i[0]Keep your files to yourself, I don't want them.\e"
	}
	
}

//This set of options sets the wallpaper up. You can replace the dialogue, but make sure to leave the \![set,wallpaper, etc.] tag at the beginning.

Select.SetWallpaper_0
{
	if LV == 0
	{
		"\![set,wallpaper,%(Wallpaperfile),center]\0\s[104]Ok, I've centered it for you.\w8\w8\s[106] Is it straight? It's hard to tell from here.\e"
	}
	else
	{
		"\![set,wallpaper,%(Wallpaperfile),center]\0\s[510]\i[0]...Ok, I've centered it.\e"
	}
	
}
Select.SetWallpaper_1
{
	if LV == 0
	{
		"\![set,wallpaper,%(Wallpaperfile),tile]\0\s[0]\s[104]Ok, I've tiled it for you.\w8\w8\s[101] Funky.\e"
	}
	else
	{
		"\![set,wallpaper,%(Wallpaperfile),center]\0\s[510]\i[0]...Ok, I've tiled it.\e"
	}
}
Select.SetWallpaper_2
{
	if LV == 0
	{
		"\![set,wallpaper,%(Wallpaperfile),stretch]\0\s[104]Ok, I've stretched it across.\w8\w8\s[106] Does it look ok?\e"
	}
	else
	{
		"\![set,wallpaper,%(Wallpaperfile),center]\0\s[510]\i[0]...Ok, I've stretched it.\e"
	}
	
}
Select.SetWallpaper_Cancel
{
	if LV == 0
	{
		"\0\s[0]Alrighty then, just let me know if you change your mind.\e"
	}
	else
	{
		"\0\s[510]\i[0]...Ok.\e"
	}
	
}


 //---- OnInstallBegin -------------------------------------------------------
 //OnInstallBegin is called when a file with an included install.txt is dropped on your ghost. This primarily means things like .nar files for other ghosts or .zip files containing balloons or shells for your ghosts. Primarily ghost-related things. Replace as you want.
 
OnInstallBegin
 {
	 lastinteracted = 0
	 if LV == 0
	 {
	"\0\s[0]Oh, you need something installed?\w8\w8\s[1070] Uh, let me see...\e"
	 }
	 elseif LVCheck == 1
	 {
		"\0\s[510]\i[0]...Ok, just a moment.\e"
	 }
	 else
	 {
		"\s[400]\i[0]Hmph.\w8 I'm not doing anything with it.\e"
		"\s[400]\i[0]Keep your files to yourself, I don't want them.\e"
	 }
 }

 //---- OnInstallComplete -------------------------------------------------------
 //OnInstallComplete runs once the file above has finished installing.
 
OnInstallComplete
 {
	"\0" //this means your main character will be speaking first, therefore the \s[] at the beginning of each line will be defining your main character's pose.
 	--
	if reference0 == "shell" //This is checking to see if what you installed is classified as a shell in the install.txt. Remember the line in install.txt that said type,ghost or type,shell? This is why that line is important, that's what tells the program what it's installing like so.
 	{	
		if reference1 == "Hypothetical Shell that doesn't actually exist" //This is the name of your shell as you defined in descript.txt in the shell folder.
		{
		"\s[0]We have installed Hypothetical Shell.\w8\1\s[10]Truncated for space.\e"
		//Replace if needed.
		}
		elseif reference1 == "Another totally real shell wow" //As in other files, you can use elseif to expand a list like this to include as many shells as you have or think you'll make. See bootend.dic for more details about elseif.
		{
		"\s[0]We have installed another totally real shell.\e"
		//Replace if needed.
		}
		else //For any other shell they may install.
		{
		"\s[0]We've installed a new shell.\w8\1\s[10]It's called  %(reference1).\e"
		//%(reference1) here would be the name of the shell they installed. Replace if needed.
		}
 	}
	elseif reference0 == "ghost" //if the type of the file they're installing is a ghost.
 	{
		if LV == 0
		{
			"\s[1070]Oh, someone else..?\w8\w8\s[101] Is this how I ended up here?\e"
		}
		else
		{
			"\s[510]\i[3]...Looks like someone else for you to-\s[510]\i[5]\w8\w8\w8\s[510]\i[0] ...talk to.\e"
		}
		//Reference1 here would be the name of the ghost. Replace as needed.
 	}
	elseif reference0 == "balloon" //if the type of file is a balloon
 	{
		if LV == 0
		{
		"\s[1070]Oh, it's a...\w8 balloon?\w8\w8\s[205] ...Doesn't look like any balloon I've ever seen, but ok.\e"
		}
		else
		{
			"\0\s[510]\i[0]...It's a new balloon.\e"
		}
		//Reference1 here would be the name of the balloon. Replace as needed.
 	}
	elseif reference0 == "plugin" //if the type of file is a plug-in, like a saori of some kind.
 	{
		if LV == 0
		{
		"\s[1070]Oh, it's a...\w8 'plugin'?\w8\w8\s[1020] Sorry kiddo, got no idea what that is.\w8\s[0] But it's installed now, so it should work for ya.\e"
		}
		else
		{
			"\0\s[510]\i[0]...It's a new plugin.\e"
		}
		//Reference1 here would be the name of the plug-in. Replace as needed.
 	}
}

//---- OnInstallFailure -------------------------------------------------------
//If the file you dropped fails to install for one reason or another.

OnInstallFailure
 {
	if LV == 0
	{
	"\0\s[200]Ah,\w4 sorry %(username),\w4 something went wrong." //this first bit of dialogue begins the explanation of what happened. You can replace this with an appropriate intro statement.
		--
		if reference0 == "unsupported" //These if statements determine how it happened to finish the piece of dialogue. This 	checks if the reason it failed is because it was unsupported.
		{
		"\s[201]I don't think that file is supported.\e"
		//Replace with whatever dialogue for an unsupported file you may come up with.
		}	
		elseif reference0 == "extraction" //This checks if the file didn't extract properly.
		{
		"\s[300]I couldn't get it to extract.\e"
		//See above but for a failed extraction.
		}
	}
	else
	{
	"\s[510]\i[6]Something went wrong." //this first bit of dialogue begins the explanation of what happened. You can replace this with an appropriate intro statement.
		--
		if reference0 == "unsupported" //These if statements determine how it happened to finish the piece of dialogue. This 	checks if the reason it failed is because it was unsupported.
		{
		"\s[510]\i[0]The file isn't supported.\e"
		//Replace with whatever dialogue for an unsupported file you may come up with.
		}	
		elseif reference0 == "extraction" //This checks if the file didn't extract properly.
		{
		"\s[510]\i[0]I couldn't extract it.\e"
		//See above but for a failed extraction.
		}

	}
 }
 
 //--OnInstallRefuse
 //If the file you dropped can't be installed with this ghost.
 
OnInstallRefuse
 {
	 if LV == 0
	 {
		"\s[106]Sorry kiddo,\w4 I can't install this.\w8\w8\s[1070] Looks like it's meant for someone else,\w4 it says %(reference0)?"
	 }
	 else
	 {
		"\s[510]\i[6]...I can't install this.\w8\w8\s[510]\i[0] It's for %(reference0)."
	 }
	//reference0 in this function refers to the ghost this file is associated with, probably the name of the ghost listed in its install.txt file. You would see this if you dropped a shell made for a different ghost on your current ghost, for example. They can't install a shell for themselves they can't use, right? Anyway, replace this with what you wish, you can use the reference0 envelope or not if you want.
 }


//**** Uninstallation ******************************************
//You may have noticed in the configuration menus in menu.dic an option to turn Uninstall on or off, as well as a greyed out option to uninstall in the right click menu. These following functions relate to the uninstallation process when it's on. Replace the dialogue in each function as we go along.


//--OnVanishSelecting
//When you select the uninstall option in the menu.

OnVanishSelecting
{
	if LV == 0
	{
	"\0\s[205]* (Is it time to say goodbye..?)\e"
	}
	else
	{
	"\0\s[400]\i[4]* (Are you done with him..?)\e"
	}
}

//OnVanishSelected
//When you decide to uninstall them. 

OnVanishSelected
{
	if LV == 0
	{
	"\0\s[0]* (You say farewell.)\w8\w8\n\n\s[110]Oh, really..?\w8\w8\s[106] Well, alright then.\w8\w8\n\n\s[1010]It was nice hanging out with you,\w4 %(username).\w8\w8 Take care of yourself!\w8\w8\e"
	}
	else
	{
		if LVCheck == 1
		{
			"\0\s[510]\i[0]* (He leaves \s[-1]quietly.)\w8\w8\e"
		}
		else
		{
			"\0\s[400]\i[2]Took you long enough.\w8\w8\e"
		}
	}
}


//--OnVanishCancel
//If you select Uninstall from the menu, but decide not to do it.

OnVanishCancel
{
	if LV == 0
	{
	"\0\s[0]* (You decide to keep him a while longer.)\e"
	}
	elseif LVCheck == 1
	{
	"\0\s[510]\i[0]* (Not just yet.)\e"
	}
	else
	{
	"\0\s[400]\i[0]* (He tries to hide his disappointed look.)\e"
	}
}

//--TalkVanishButtonHold
//I believe this calls if you click and hold the button on either the menu option for uninstalling them, or possibly the option itself. Be careful if you're trying to test this! You'd hate to uninstall all your hard work, right? I'd probably just make sure the dialogue looks right via Script Input and leave it at that.
//[Zi] I've tested this and can't seem to get this dialogue to appear. Ah well.

TalkVanishButtonHold
{
	if LV == 0
	{
	"\0\s[206]* (He seems agitated.)\e"
	}
	elseif LVCheck == 1
	{
	"\0\s[510]\i[0]* (He stays silent.)\e"
	}
	else
	{
	"\0\s[410]\i[0]* (He seems agitated.)\e"
	}
}


 //---- OnVanished -----------------------------------------
 //OnVanished is a weird little function. Basically, if you uninstall a ghost, it'll automatically go back to the last ghost you were using. They'll then say this line of dialogue about the ghost you just uninstalled. This is very annoying to test as you can imagine! But unlikely to come up super often, I bet. You might as well be thorough and fill this out as well. Make sure to set up both their poses immediately so they both appear when it switches back to them.
 
OnVanished
 {
	if LV == 0
	{
	"\0\s[206]Oh,\w4 is %(reference0) gone..?\w8\w8\s[204] ...I hope they're alright...\e"
	}
	elseif LVCheck == 1
	{
	"\0\s[510]\i[6].\w8.\w8.\w8.\w8.\w8\s[510]\i[0]\e"
	}
	else
	{
	"\0\s[400]\i[4].\w8.\w8.\w8\s[400]\i[0]Just like that, huh?\w8\w8\s[400]\i[3] You're a cruel person, %(username).\e"
	}
}


//**** Mail Checking *******************************************************************
//The next set of functions have to do with checking your email. This function is a slippery little thing - I've had people test it following my directions and have it work, but I've never actually gotten it to report back the right amount of emails myself. Very frustrating. But perhaps you'll have more luck with it, haha.


//*********************Advanced User Info*******************************
//These text boxes are meant for those who have an interest in expanding or using more complicated functions in their ghost. Also for my own reference in the future since I plan on using this template too, haha. If you have no interest in any of this and just want to keep things as simple as possible, feel free to skip these boxes.

//If you want to set up your ghost to check your mail so you can more easily check these messages instead of just writing the dialogue and moving on, it's a little bit tricky, but not super hard.
//Basically, you're going to need to find your POP settings for your email account. Googling your provider + POP should bring it up pretty quick. Once you have the info, you'll go to the right click menu, options, then preferences. A big menu should pop up, and you should see POP in the left side. Click that, and input all the info from the POP page you looked up for your account. Tell your ghost to check your email, and if it works, then congratulations!
//***********************************************************************


 //---- OnBIFFBegin ---------------------------------------------------------------
 //When you first tell the ghost to check your email. An intro statement, if you will. Replace with whatever you like.
 
 
 
//[Zi] Note to self, at some point I mixed up the breaking and broken dialogue, I think I fixed all the numbers so that it picks the correct one now, but if it acts weird then maybe double check that... 
OnBIFFBegin
 {
	lastinteracted = 0
	if LV == 0
	{
		"\0\s[101]Uh,\w4 alright,\w4 sure.\w8\w8\s[1070] Give me just a moment...\e"
	}
	else
	{
		if broken == 1
		{
			"\s[510]\i[0]* (He nods, and begins checking for new emails.)\e"
		}
		else
		{
			"\s[510]\i[0]...Ok.\e"
		}
	} 

 }

 
 //---- OnBIFFComplete ---------------------------------------------------------------
 //After they've succeeded in checking your mail, they'll report back with how many new emails you have. Replace the dialogue with whatever would be character appropriate.
 
OnBIFFComplete
 {
	if reference0 == 0 //If you have no new mail.
 	{
 		if LV == 0
	{
		"\0\s[106]Nope, looks like there's nothing new.\e"
	}
	else
	{
			"\0\s[510]\i[6]...No new emails.\e"
	} 
	}
	elseif reference0 == 1 //If you have one new email.
 	{
 		if LV == 0
	{
		"\0\s[0]You've got one new email."
	}
	else
	{
		"\0\s[510]\i[6]...One new email.\e"
	} 
	}
	elseif reference0 > 1 && reference0 < 11 //If you have more than one email but less than eleven.
	{
		if LV == 0
	{
		"\0\s[0]You've got %(reference0) new emails.\w8\s[103] Probably shouldn't let those pile up too much, huh?\e"
	}
	else
	{
			"\0\s[510]\i[6]...%(reference0) new emails.\e"
	} 
		//reference0 here stores the amount of new email it finds.
	}
	elseif reference0 > 100 //If there are over one hundred new emails.
	{
		if LV == 0
	{
		"\0\s[106]You've uh...\w8\s[107] got a lot of new emails,\w4 kiddo.\w8\w8\s[208] I'd say more than 100,\w4 at least.\e"
	}
	else
	{
			"\0\s[510]\i[6]...Over 100 new emails.\e"
	} 
	}
 	else //For any other number of emails.
	{
		if LV == 0
		{
		"\0\s[0]You've got %(reference0) new emails.\e"
		}
		else
		{
				"\0\s[510]\i[6]...%(reference0) new emails.\e"
		} 
 	}
}

 //---- OnBIFFFailure ---------------------------------------------------------------
 //Various error messages that can happen when your ghost tries to connect to an email account. Replace the dialogue as appropriate.
 
OnBIFFFailure
 {
	if reference0 == "timeout" //If the connection times out.
 	{
 		if LV == 0
		{
			"\0\s[106]Sorry, looks like the connection timed out.\w8\w8 Maybe try again later?\e"
		}
		else
		{
				"\0\s[510]\i[0]...Sorry, the connection timed out.\e"
		} 
	}
 	elseif reference0 == "kick" //If the account blocks the ghost from accessing it. I think gmail may do this at first to try and protect your account from unapproved applications using it. It gives you the option to change the setting in the warning email they send you about it.
	{
		 if LV == 0
		{
			"\0\s[106]Sorry, it won't let me in.\w8\w8 Maybe double check your email settings?\e"
		}
		else
		{
				"\0\s[510]\i[0]...Sorry, I can't access your inbox.\e"
		}
	}
	elseif reference0 == "defect" //If the information put into the POP menu is wrong.
 	{
		if LV == 0
		{
			"\0\s[106]Sorry, looks like something isn't set up quite right.\w8\w8\s[206] Maybe double check your POP settings?\![open,configurationdialog]\e"
		}
		else
		{
				"\0\s[510]\i[0]...Sorry, the POP settings aren't right.\![open,configurationdialog]\e"
		} 
	
	}
 	else //if there's some other kind of error.
	{	
		if LV == 0
		{
			"\0\s[106]Sorry, all I've got is some kind of error message.\n\n'%(reference[0])'.\e"
		}
		else
		{
				"\0\s[510]\i[0]...Sorry, I got an error.\w8\n\n'%(reference[0])'.\e"
		} 
		
		
		//reference0 here stores the error message the email server would have given the ghost, hopefully.
 	}
}


 //**** Network Update *****************************************************************
 //The following functions are for the Network Update of your ghost, where it will connect to your website, check for new files, and update itself. This is a handy feature, but tricky to set up. If you have no intention of using Network Update, you can disregard these, but it won't hurt to fill them out anyway just in case. More details about how to set up Network Update are in the walkthrough (http://www.ashido.com/ukagaka/walkthrough.html).
 
 //Replace the dialogue in the next set of functions as appropriate. You can easily test these messages using the Developer panel. Go to Open at the bottom of it and choose one of the four Update Event options to simulate a Ghost's update process. I highly recommend doing this at LEAST once if you're going to use this feature to make sure it flows well.

OnUpdateCheckComplete
{
	if reference0 != "none" //reference0 here will say 'none' if there are no new files, and will be a number if there are new files. So if reference0 is anything other than 'none', tell the user there's an update
	{
		updateavailable = 1
	}
	else //just in case this variable ends up not being reset...
	{
		updateavailable = 0
	}
	
}


 
 //---- OnUpdateBegin -------------------------------------------------------------
 //When you first tell your ghost to update. An introductory statement, like OnBIFFBegin above. Replace or add more dialogue as appropriate. Note that they end with \w8 to give a little breather time.
 
OnUpdateBegin
 {
	updateavailable = 0
	lastinteracted = 0
	if LV == 0
	{
		"\0\s[110]Updates?\w8\w8\s[1010] Sure,\w4 I can check.\w8\w8\s[1070] Let me see...\w8\e"
	}
	else
	{
		if LVCheck == 1
		{
			"\0\s[510]\i[0]* (He nods and begins checking for updates.)\w8\e"
		}
		else
		{
			"\0\s[400]\i[0]Hmph.\w8\w8 Maybe I'll get lucky and there'll be an update that lets me punch you instead.\w8\e"
		}
	}
 }

 //---- OnUpdateReady ---------------------------------------------------------
 //If the ghost finds new files to update with.
 
OnUpdateReady
 {
	reference[0] += 1 //So I'm guessing that this counts starting from 0, so it always shows as 1 less file than it is... this fixes that.
	if LV == 0
	{
		"\0\s[1010]Looks like there are %(reference[0]) new files.\w8\e"
	}
	else
	{
		if LVCheck == 1
		{
			"\0\s[510]\i[6]There are %(reference[0]) new files.\w8\e"
		}
		else
		{
			"\0\s[400]\i[4]There's something new after all.\w8\e"
		}
	}
	 
	 
	 
	//"\1\s[10]There are %(reference[0]) new files.\w8\e"
	
	//reference0 here storing the number of new files. You don't have to use it if you don't want to.
 }


//---- OnUpdateComplete -------------------------------------------------------------
//When the ghost has finished the update process.

OnUpdateComplete
 {
	SeePC = "OFF"
	SeeNowPlaying = "OFF"
	SeeDiscord = "OFF"
	SongMemories = ""
	SongList = "-"
	FoobarOpen = 0
	FoobarMin = 0
	WinampOpen = 0
	WinampMin = 0
	ItunesOpen = 0
	ItunesMin = 0
	SpotifyOpen = 0
	SpotifyMin = 0
	ActuallySpotify = 0
	VLCOpen = 0
	VLCMin = 0
	DiscordOpen = 0
	DiscordMin = 0
	DiscordorChrome = 0
	DevPalleteOpen = 0
	WhichDev = 0
	EVNovaOpen = 0
	EVNovaMin = 0
	askedabouttimelines = 0
	AlreadyResponded = 0
	SpokeFirst = 0
	TempSong = ""
	AzuraSongList = "-"
	speech = ""
	recentpets = 0
	autoupdate = "OFF"
	updateavailable = 0
	if reference0 == "none" //if the ghost had nothing to update with
 	{
 		if LV == 0
		{
			"\0\s[106]Sorry kiddo,\w4 looks like there are no new files.\e"
		}
		else
		{
			if LVCheck == 1
			{
				"\0\s[510]\i[6]There are no new files.\e"
			}
			else
			{
				"\0\s[410]\i[2]There's nothing new.\w8\w8\s[400]\i[0] Stop wasting my time.\e"
			}
	}
	
	}
 	else //if the ghost has updated successfully
	{
		if LV == 0
		{
			"\0\s[1010]Update complete!\w8\w8\s[0] Do you want to see the changelog?\n\n\![*]\q[Yes,seechangelog]\n\![*]\q[No,nochangelog]\e"
		}
		else
		{
			if LVCheck == 1
			{
				"\0\s[510]\i[0]...Update complete.\w8\w8\s[510]\i[6] Do you want to see the changelog?\n\n\![*]\q[Yes,seechangelog]\n\![*]\q[No,nochangelog]\e"
			}
			else
			{
				"\0\s[400]\i[2]Update complete.\e" //No he's not going to offer you the changelog lol
			}
		}
 	}
}

 //---- OnUpdateFailure -------------------------------------------------------------
 //Error messages if the update process fails in some way.
 
OnUpdateFailure
 {
	if reference0 == "timeout" //if the connection times out
 	{
 		if LV == 0
		{
			"\0\s[106]Sorry kiddo,\w4\s[107] looks like the connection timed out.\w8\w8\s[0] Maybe try again later?\e"
		}
		else
		{
			if LVCheck == 1
			{
				"\0\s[510]\i[0]...Sorry,\w4 the connection timed out.\e"
			}
			else
			{
				"\0\s[400]\i[0]The connection timed out.\e"
			}
		}
	}
 	elseif reference0 == "md5 miss" //This is if the files don't match properly to updates.dau, I think. For example, if a file only partially transferred or somehow got corrupted. This may also come up if you didn't set up the directory and updates.dau properly as outlined on the walkthrough page.
	{
		if LV == 0
		{
			"\0\s[107]Sorry kiddo, something went wrong.\w8\w8\s[106] The MD5 values don't match.\w8\w8\s[0] You should let Zichqec know so she can fix it.\e"
		}
		else
		{
			if LVCheck == 1
			{
				"\0\s[510]\i[5]...I'm sorry, the files don't match.\w8\w8\s[510]\i[6] You should let Zichqec know so she can fix it.\e"
			}
			else
			{
				"\0\s[400]\i[0]The files don't match.\w8\w8\s[400]\i[2] You'll have to talk to Zichqec about that, I can't do anything for you."
			}
		}

 	}
	else //for other errors
 	{
 		if LV == 0
		{
			"\0\s[106]Sorry kiddo, something went wrong.\w8\w8\s[1070] All it says is '%(reference0).\w8\w8\s[106] Maybe you should let Zichqec know about that?"
		}
		else
		{
			if LVCheck == 1
			{
				"\0\s[510]\i[2]...I'm sorry, something went wrong.\w8\w8 All it says is '%(reference0).\w8\w8\s[510]\i[0] You might try talking to Zichqec about that.\e"
			}
			else
			{
				"\0\s[410]\i[0]Something went wrong.\w8\w8 All it says is '%(reference0).\w8\w8\s[400]\i[2] Maybe you should try asking Zichqec about that.\e"
			}
		}
	}
 }


 //---- OnUpdate.OnDownloadBegin ------------------------------------------------
 //This dialogue plays when they begin downloading a new file. WARNING: if you have big plans for your ghost and are going to make a lot of new image files for it (like... i do, cough) at some point, you're going to want to make these messages very brief, if you even have them at all. The reason is because the user will see these messages for EVERY individual file that they have to download, and if you have, say, 50 pna files in the queue, they will see this dialogue 50 times. Not only that, but displaying the dialogue takes some time, so it also extends the download process.
 //If you aren't planning on adding a ton of files at any point, or are only intending to update the .dic files which will be at most a handful of files, then you can go ahead and put a short message in here. But if your ghost is going to grow in leaps and bounds as you work on it, try and keep these messages as brief as possible to make the process that much faster for your user.
 
OnUpdate.OnDownloadBegin
 {
 	if "aitalk" _in_ reference0 //this is checking to see what kind of files are being downloaded. In this case, it's looking for files called aitalk in the download queue. If you want to have a specialized bit of dialogue for each type of file, you can expand this with elseifs and by replacing aitalk with whatever file you have in mind. Keep in mind my warning above though.
 	{
 		if LV == 0
		{
			"\0\s[1010]Looks like something new to say randomly.\e"
		}
		else
		{
			if LVCheck == 1
			{
				"\0\s[510]\i[2]...New random dialogue...\e"
			}
			else
			{
				"\0\s[410]\i[2]Hey, it's some new dialogue.\w8\w8\s[400]\i[2] Maybe you could see some of it if you didn't hit me.\e"
			}
		}
 	}
	if "mouse" _in_ reference0
	{
		if LV == 0
		{
			"\s[106]Looks like there might be new petting dialogue.\w8\w8\s[107] If uh, that's what you want to do.\e"
		}
		else
		{
			if LVCheck == 1
			{
				"\s[510]\i[6]* (Looks like there might be new dialogue for petting or hitting him...\w8\w8 he seems unhappy about it.)\e"
			}
			else
			{
				"\s[400]\i[0]Great, look at that.\w8\w8 Something new might happen if you hit me, hooray.\e"
			}
		}
	}
 	else //for files not specified above
 	{
		if LV == 0
		{
			"\s[1070]...\e"
		}
		else
		{
			if LVCheck == 1
			{
				"\s[510]\i[0]...\e"
			}
			else
			{
				"\s[400]\i[0]...\e"
			}
		}
	}
 }
 
Select.seechangelog
{
	OnChangeLog
}


Select.nochangelog
{
	if LV == 0
	{
		"\s[1010]Ok,\w4 if you change your mind just let me know.\e"
	}
	else
	{
		"\s[510]\i[0]...Ok.\w8\w8 Just tell me if you want to see it later.\e"
	}
}
 
OnChangeLog //[Zi] haha I hope I remember to update this
{
	"\_q\b2                            -Version 1.0-\n\n/
	+Added S\n/
	+Added Red Hoodie shell\n/
	+Added S Skeleton Balloon\n/
	+Added menus\n/
	+Added sass\n/
	+Added update functionality\n/
	*Changed thumbnail picture and right click menu\n/
	\n\n\![*]\q[Back to functions,dosomething]\n\![*]\q[Close,cancelnodialogue]\e"
}
 
 
 
 

 //---- OnUpdate.OnMD5CompareBegin ---------------------------------------------------
 //This checks the hash value of the files in the directory and the files listed in updates.dau, I believe. It will do this for every file the user downloads, so I again remind you of my warning above! For every file they download, they will have to go through three sets of unique dialogue, and it can get old fast. Not to mention it slows things down a lot. Again, I would recommend keeping this very brief, if not just replacing it with "..." instead.
 
OnUpdate.OnMD5CompareBegin
 {
	 if LV == 0
	{
		"\s[1070]...\e"
	}
	else
	{
		if LVCheck == 1
		{
			"\s\[1590]\[2]...\e"
			"\s\[1590]\[0]...\e"
		}
		else
		{
			"\s\[1552]\[0]...\e"
			"\s\[1560]\[0]...\e"
		}
	}
	
 }

 //---- OnUpdate.OnMD5CompareComplete --------------------------------------------------
 //After they've checked the MD5 value and it matches. See above about the length of this message.
 
OnUpdate.OnMD5CompareComplete
 {
	""
}

 //---- OnUpdate.OnMD5CompareFailure ---------------------------------------------------
 //If the MD5 values don't match. If you follow my Network Update directions carefully, this shouldn't come up very often, but don't be surprised if you run into it every now and then by forgetting a file here or there.
 
OnUpdate.OnMD5CompareFailure
 {
	"\0\s[1070]The MD5 values don't match.\w8\e"
 }

 
 
 //**** Headlines and RSS Feeds *********************************************************
 //This section has to do with the RSS feeds option on your ghost. If you drag and drop a url on them, they'll add it to a list you can access via the right-click menu, and they'll tell you the last set of updates for the site. This works best with RSS feeds I think, but you can experiment with other sites to see how it works.
 //The headlines you see under the tab are in the headline folder under your base SSP install. So it'd be SSP\headlines. You should see the folders for each linked headline in there. I've very rarely ever messed with them or used them. Likewise, your RSS feeds you've added will be under SSP\rss and I don't think that option will appear in the headlines/RSS tab until you add an RSS feed to a ghost. If you want to clear your RSS feeds or headlines, just delete either of those two folders.
 

 //---- OnHeadlinesenseBegin -----------------------------------------------------------
 //This dialogue comes up when you select something in your RSS feed as an intro, like the other On[Thing]Begin things we've seen so far. Replace as you need.
 
OnHeadlinesenseBegin
 {
	lastinteracted = 0
	if LV == 0
	{
		"\0\s[1070]%(reference0)?\w8 Uh, let me see here...\e"
	}
	elseif LVCheck == 1
	{
		"\0\s[510]\i[0]%(reference0)...\e"
	}
	else
	{
		"\s[400]\i[0]Hmph.\e"
	}
 }

 //---- OnHeadlinesense.OnFind --------------------------------------------------------
 //This actually displays the feed. My set-up for it is pretty simple but gets the job done, it just has the name of the feed, a line space, then a list of links. You shouldn't have to edit any of these and I probably wouldn't honestly.
 
OnHeadlinesense.OnFind
{
	//----  What page You're on
 	if reference2 == "First" || reference2 == "First and Last"
	{
 		//---- First page
		"\0\b2%(reference0):"
 	}
	elseif reference2 == "Next"
 	{
		//---- Second page
 		"\0\b2%(reference0):"
	}
 	elseif reference2 == "Last"
	{
 		//---- Last page
		"\0\b2%(reference0):"
 	}
	--
 	//---- Headlines
	"\n\n%reference3\n"
 	--
	//---- Pages
 	if reference2 == "First" || reference2 == "Next"
	{
 		//---- Bottom buttons
		"\![*]\q[Next,] - \![*]\q[Stop,Headline_Cancel]\e"
 	}
	else
 	{
		//---- Bottom buttons
 		"\![*]\q[Stop,Headline_Cancel]\e"
 	}
	
}


Select.Headline_Cancel
{
	if LV == 0
	{
		"\0\s[0]Headline viewing complete.\e"
	}
	elseif LVCheck == 1
	{
		"\0\s[510]\i[0]Headline viewing complete.\e"
	}
	else
	{
		"\0\s[400]\i[0]Headline viewing complete.\e"
	}
	//Actually I take that back, you can change this line of dialogue here for when you're done looking at a headline to something else if you want.
}

 //---- OnHeadlinesenseComplete -------------------------------------------------------
 //This is an odd function. As far as I can gather, this is if the headline information gathering was successful, but there's no new information to report. Possibly if none of the feeds have updated? I'm a little fuzzy on this one.
 
OnHeadlinesenseComplete
 {
	"\0\s[0]There's no update. %(reference0).\e"
	//reference0 here supposedly always stores the value no update? Again this isn't hugely clear from what I can see. Anyway, this dialogue has never come up for me while using a ghost, so perhaps someone else who runs into it can offer more clarity. For now, you can leave it as is, or write up something else in here if you like.
 }

 //---- OnHeadlinesenseFailure ---------------------------------------------------------
 //This one is more clearly for when the ghost can't get the information it needs about the RSS feed in question.
 
OnHeadlinesenseFailure
  {  
	if LV == 0
	{
		"\0\s[106]"
		--
	}
	elseif LVCheck == 1
	{
		"\0\s[510]\i[6]"
		--
	}
	else
	{
		"\0\s[410]\i[2]"
		--
	}
	--
	"The feed can't updated.\w8\n\n[half]" //This sets up an exchange between the two characters. The final part of the exchange continues below in the if statements.
 	--
	if reference0 == "can't download" //if they can't download the rss feed for some reason
 	{
		"It can't be downloaded."
 	}
	elseif reference0 == "can't analyze" //if they can't read the file for some reason
 	{
		"It can't be analyzed."
 	}
	else //other errors
 	{
		"I don't know why."
 	}
	--
 	"\e" //And here is the final /e, so you don't need to include it in the if statements above. Another one of the very rare cases this occurs.
}


//**** Ghost Creation and Updating ******************************************************
//These next few are related to creating updates2.dau and .nar files for your ghosts. You'll want to at least make a .nar to distribute your ghost, and if you're going to be using Network Update, you'll also want to make updates2.dau as well. It's unlikely that anyone other than you will see these messages, but you should be thorough and replace them anyway.
//Primarily, you will see these messages when you're creating your updates file or updating your ghost.nar file. More on this on the walkthrough page. However, you could potentially drag and drop another ghost's folder onto them, and they'll also create a nar for that as well, though I've never done that myself. I really wouldn't mess around too much with this. Just follow the directions on the walkthrough page and fill out the dialogue here as required.


 //---- OnUpdatedataCreating -----------------------------------------------------------
 //This will begin the creation of the updates2.dau file. Not unlike the other beginning functions you've seen throughout this file. Replace with whatever you like.
 
OnUpdatedataCreating
 {
	"\0\s[1010]Creating the updates2.dau file.\e"
 }

 //---- OnUpdatedataCreated -------------------------------------------------------------
 //After the updates2.dau file has been successfully created. Replace with whatever.
 
OnUpdatedataCreated
 {
	"\0\s[1010]Successfully created updates2.dau.\e"
 }

 //---- OnNarCreating ------------------------------------------------------------------
 //Along the same lines, this is when they begin creating a .nar file for your ghost. .nars are the format most ghosts will be in, something similar to a .zip file, and this is the format you'll be distributing your ghost in most likely. Replace the dialogue with whatever you like.
 
OnNarCreating
 {
	"\0\s[1010]Creating the .nar file.\e"
 }

 //---- OnNarCreated -------------------------------------------------------------------
 //After the nar file has been successfully created.
 
OnNarCreated
 {
	"\0\s[1010]Successfully created '%(reference0)'.\e"
	//reference0 here will be the filename of the nar you just created. It should drop the nar in the folder where all your ghosts are listed, ssp\ghost\.
 }

 //****　Clock Synchronization　**********************************************************
//**** 미카 (http://homepage2.nifty.com/ko-ki/)의 사전을 참고했습니다．감사＞こ 나무 성
//This set of functions was done by the above Korean coder, I think. I'll leave their comment there untouched. 
//If you right click your ghost and go into the Utilities menu, there's an option called NTP Time Sync. That's what this is for!
 

 //----OnSNTPBegin ------------------------------------------------------------------
 //As with the others you've seen above, this is the beginning bit of dialogue for this process.
 
OnSNTPBegin
 {
	lastinteracted = 0
	if LV == 0
	{
		"\0\s[110]The time..?\w8\w8\s[1070] Uh,\w4 let me see...\w8\e"
	}
	else
	{
		if broken == 1
		{
			"\s[510]\i[0]* (He nods and begins checking the time online.)\w8\e"
		}
		else
		{
			"\s[510]\i[0]...Ok.\e"
		}
	} 
	
	//"\0\s[0]We're going to check the time.\w9\1\s[10]We are connecting to %(reference0).\e"
	//%(reference0) here is the site they'll be using to check your clock. I wouldn't worry too much about what the site is or where it's defined, it should be accurate. Replace the dialogue with what you need, you don't have to use reference0 if you don't want to.
 }


 //----　OnSNTPCompare----------------------------------------------------------
 //Now the ghost is going to compare the current time of your computer's system to the time of the site they connected to. Most of this you don't have to touch, just replace the dialogue as indicated.
 
OnSNTPCompare
 {
 	"Retrieved Time: %reference1[1]/%reference1[2]/%reference1[0] %reference1[3]:%reference1[4]:%reference1[5]\w2\n/
	Your Time: %reference2[1]/%reference2[2]/%reference2[0] %reference2[3]:%reference2[4]:%reference2[5]\w2\n/
 	\n"
	--
 	if reference3 == 0 //if your clock is already accurate.
	{
		if LV == 0
		{
			"\s[1010]Your clock is accurate.\e"
		}
		else
		{
				"\s[510]\i[0]...Your clock is accurate.\e"
		} 
			
 		//"\0\s[0]Your clock is accurate.\e" //replace this with whatever message you like.
	}
 	else //if your clock doesn't match
	{
		
		if LV == 0
		{
			"\0\s[0]Your clock doesn't match.\w8\nShould I fix it?\n\n[half]/
			\![*]\q[Fix it,OnSNTPEXECUTE]\n/
			\![*]\q[Leave it alone,OnSNTPCANCEL]\e"
		}
		else
		{
			"\0\s[510]\i[6]Your clock doesn't match.\w8\nShould I fix it?\n\n[half]/
			\![*]\q[Fix it,OnSNTPEXECUTE]\n/
			\![*]\q[Leave it alone,OnSNTPCANCEL]\e"
		}	 
		
		
		
	//you can redo this top line up to the \n\n[half] part, but leave the \q options alone.
 		"\0\s[0]Your clock doesn't match.\w8\nShould I fix it?\n\n[half]/
		\![*]\q[Fix it,OnSNTPEXECUTE]\n/
 		\![*]\q[Leave it alone,OnSNTPCANCEL]\e"
	}
 }

 //----　OnSNTPEXECUTE ------------------------------------------------------------------
 ////After they've fixed your clock. Note the \6 tag - that's what actually changes the clock. Do not remove that tag. Replace the rest with what you want.
 
OnSNTPEXECUTE
 {
	 
	if LV == 0
	{
		"\0\s[1010]\6Alright, should be all good now.\e"
	}
	else
	{
		if broken == 1
		{
			"\0\s[510]\i[0]\6* (He silently fixes the clock.)\e"
		}
		else
		{
			"\0\s[510]\i[0]\6There, it should be correct now.\e"
		}
	} 
	
	
	//"\0\s[0]The clock has been fixed.\6\e"
 }

 //----　OnSNTPCANCEL ------------------------------------------------------------------
 //If you decide not to change your clock when they tell you it doesn't match. Replace with what you want.
 
OnSNTPCANCEL
 {
	if LV == 0
	{
		"\0\s[1010]Ok, let me know if you change your mind.\e"
	}
	else
	{
		if broken == 1
		{
			"\0\s[510]\i[0]...\e"
		}
		else
		{
			"\0\s[510]\i[0]...Ok.\e"
		}
	} 
	
	//"\0\s[0]The user decided not to change the clock.\e"
 }

 //----　OnSNTPFailure ------------------------------------------------------------------
 //If they can't change the clock for some reason, possibly because the site timed out but it could be anything. Replace with what you want.
 
OnSNTPFailure
 {
	if LV == 0
	{
		"\0\s[206]Ah, sorry, it looks like something went wrong.\w8\w8\s[106] Maybe try again later?\e"
	}
	else
	{
			"\0\s[510]\i[0]...Sorry,\w4\s[510]\i[5] something went wrong.\e"
	} 
	
	
	//"\0\s[0]The clock wasn't changed.\w8\1\s[10]Something went wrong.\e"
 }


 //--OnDisplayChange----------------------------------------------------------------
 //This is when you change the resolution of your monitor while your ghost is running. Don't mess with any of this, it should be fine as is.
 
OnDisplayChange
 {
	displaybpp    = reference0
 	displaywidth  = reference1
	displayheight = reference2
 }



 //---- OnScreenSaverStart -------------------------------------------------------
 //This happens if your ghost is running when your screensaver starts up. This and OnScreenSaverEnd are optional, you can delete them both if you like. In fact, you can just delete the rest of the functions from this point on if you're tired and lazy, but we're close to the end, so don't give up yet!
 
OnScreenSaverStart
 {
	if LV == 0 //Friendship routes
	{
		"\0\s[1100]Uh, what's that?\e"
	}
	else
	{
		""
	}
	//Replace with what you want.
 }

 //---- OnScreenSaverEnd -------------------------------------------------------
 //When you wiggle your mouse and stop your screensaver.
 
OnScreenSaverEnd
 {
		if LV == 0 //Friendship routes
	{
		"\0\s[1100]...Um.\w8 That was uh.\w8\s[510]\i[5] Quite the experience.\e"
	}
	
	elseif LV > 0 //Negative routes
	{
		if LVCheck == 1 //know his secrets
		{
			"\0\s[510]\i[5]* (He shudders.)\e"
		}
		else //Just mean for no reason
		{
			"\0\s[400]\i[1]* (He tries to hide his trembling.)"
		}
	}


	//Replace with what you want.
 }


 //---- OnApplicationBoot ---------------------------------------------------------------
 //This supposedly is for when a program starts but I have honestly never gotten it to work or seen it happen. I wouldn't sweat these three related functions too much.
/*
OnApplicationBoot
 {
	"\0\s[0]%(reference0) just started.\e"
 }

 //---- OnApplicationClose --------------------------------------------------------------
 //See above, but for the program closing.
 
OnApplicationClose
 {
	"\0\s[0]%(reference0) has closed.\e"
 }

 //---- OnApplicationFileOpen ----------------------------------------------------------
 //I believe this is for when the program detailed above opens a document file of some kind.
 
OnApplicationFileOpen
 {
	"\0\s[0]Opening %(reference1).\e"
 }
*/



 //---- OnBatteryLow -----------------------------------------------------------------
 //This is for when your laptop's battery is low. I don't have a laptop, so I've never really been able to test this. Perhaps someone with a laptop can report back to me with more details about the battery related functions?
 
OnBatteryLow
 {
	 if LV == 0
	 {
	"\s[202]Hey,\w4 looks like your battery is getting low, there.\w8\w8\s[200] I don't want to find out what happens if I'm still here and your computer shuts down.\e"
	 }
	//reference0 here is the amount of battery power. Replace as you like.
 }
 
 //--OnBatteryCritical-----------------------------------------------------------------
 //Likewise, this is for if your battery is REALLY low. Replace as you like.
 
OnBatteryCritical
{
	if LV == 0
	{
	"\0\s[201]Uh,\w4 hey,\w4 your battery is REALLY low.\w8\w8\s[300] If you can't get to a charger,\w4\s[200] could you at least send me home?\w8\w8\s[302] I'm uh...\w8\w8\s[510]\i[5] not fond of the dark.\e"
	}
}
 
//--OnBatteryNotify--------------------------------------------------------------------
//This I believe checks to see if your laptop is plugged in or not. Originally this bit of code was commented out, so I suspect it may not work. It may need a plug-in of some sort. Either way, you can delete it or fill it out as you like.
//Actually investigating this, leaving this in makes it so the ghost updates you about whether or not your computer is plugged-in constantly, which makes no sense for desktops. No doubt why it was commented out. Just leave it be.

/*
OnBatteryNotify
{
	if reference2 == "online" //If your laptop is plugged in
	{
		"\0\s[0]The laptop is plugged in.\e"
	}
	elseif reference2 == "offline" //If you unplug your laptop
	{
		"\1\s[10]The laptop is not plugged in.\w8\0\s[0]Watch your battery.\e"
	}
}
*/


 //---- OnSysResourceLow -----------------------------------------------------------
 //A notification for if your system resources are low. I've never seen this happen and my system resources can run pretty low sometimes (woo windows xp woooo), so I suspect this one also may not work without the right plug-in, but I could be wrong. You might as well fill it out.
 
OnSysResourceLow
 {
	 if LV == 0
	{
	"\0\s[0]Hey uh,\w4\s[202] this says something about your system resources being low.\w8\w8\s[107] I have no idea what that means,\w4\s[106] do you?\e"
	}
 }


 //--OnShellScaling
 //This is a function however that you may actually use! Or at least, I often use it when desktop space is at a premium (which it always is on my computer). If you right click the ghost, you can go to Shell Scaling, which lists a number of different percentages. Those are the percentages your ghost can be scaled in size up or down. You can try them out yourself to get an idea of what I mean. I often scale mine down to get them out of the way sometimes. This adds dialogue whenever you change their scale. You don't have to do this if you don't want to, in which case they will just stay silent when you size them up or down. If that's what you want, just delete the whole thing.
 
OnShellScaling
{
	lastinteracted = 0
	if LV == 0
	{
	if reference0 >= 133 //this number is the percentage that you're scaling the ghost up to. In this case, it's 150%.
	{
		"\0\s[1100]Whoa.\w8\w8\s[106] Uh, hey there.\w8\w8\s[104] I'm not used to being so tall.\e"
		
		if reference0 >= 166 //Has a chance to trigger instead of the above dialogue, if you make him way larger (his art starts to look fuzzy, geddit)
		{
		"\0\s[1100]Whoa.\w8\w8\s[106] Uh, I'm starting to feel a little fuzzy, kiddo.\e"
		}
	}
	elseif reference0 == 100 //this is 100%
	{
		"\0\s[107]Whew.\w8\w8\s[106] Thanks,\w4 %(username).\e"
	}
	elseif reference0 <= 80 //this is 50%. You can add any percentage that's listed under shell scaling if you want using more elseifs.
	{
		"\0\s[106]...Uh,\w4 hey,\w4 aren't I small enough already?\e"
	}
	}
}